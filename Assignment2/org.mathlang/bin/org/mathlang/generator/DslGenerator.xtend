/*
 * generated by Xtext 2.25.0
 */
package org.mathlang.generator

import org.eclipse.emf.ecore.resource.Resource
import org.eclipse.xtext.generator.AbstractGenerator
import org.eclipse.xtext.generator.IFileSystemAccess2
import org.eclipse.xtext.generator.IGeneratorContext
import org.mathlang.dsl.MathExp
import javax.swing.JOptionPane
import org.mathlang.dsl.Plus
import org.mathlang.dsl.Minus
import org.mathlang.dsl.Mult
import org.mathlang.dsl.Div
import org.mathlang.dsl.Primary
import org.mathlang.dsl.Exp
import org.mathlang.dsl.Parenthesis

/**
 * Generates code from your model files on save.
 * 
 * See https://www.eclipse.org/Xtext/documentation/303_runtime_concepts.html#code-generation
 */
class DslGenerator extends AbstractGenerator {

	override void doGenerate(Resource resource, IFileSystemAccess2 fsa, IGeneratorContext context) {
		val math = resource.allContents.filter(MathExp).next
		val result = math.compute
		System.out.println("Math expression = "+math.display)
		// For +1 score, replace with hovering, see Bettini Chapter 8
		JOptionPane.showMessageDialog(null, "result = "+result,"Math Language", JOptionPane.INFORMATION_MESSAGE)
	}
	
	//
	// Compute function: computes value of expression
	//
	
	def static int compute(MathExp math) { 
		math.exp.computeExp
	}
	
	def static int computeExp(Exp exp) {
		val left = exp.left.computePrim
		switch exp.operator {
			Plus: left+exp.right.computeExp
			Minus: left-exp.right.computeExp
			Mult: left*exp.right.computeExp
			Div: left/exp.right.computeExp
			default: left
		}
	}
	
	def static int computePrim(Primary factor) { 
		 factor.computeEl
	}
	
	def dispatch static int computeEl(Parenthesis pa) {
		computeExp(pa.exp)	
	}
	
	def dispatch static int computeEl(org.mathlang.dsl.Number num) {
		num.value
	}

	//
	// Display function: show complete syntax tree
	//

	def CharSequence display(MathExp math) '''Math[«math.exp.displayExp»]'''
	def CharSequence displayExp(Exp exp) '''Exp[«exp.left.displayPrim»«IF exp.operator !== null »,«exp.operator?.displayOp»,«exp.right?.displayExp»«ENDIF»]'''
	def dispatch String displayOp(Plus op)  { "+" }
	def dispatch String displayOp(Minus op) { "-" }
	def dispatch String displayOp(Mult op) { "*" }
	def dispatch String displayOp(Div op) { "/" }
	def dispatch CharSequence displayPrim(Parenthesis paren) '''Parenthesis[«paren.exp.displayExp»]'''
	def dispatch CharSequence displayPrim(org.mathlang.dsl.Number num) '''Num[«num.value»]'''
	
}
